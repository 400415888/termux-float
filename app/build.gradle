apply plugin: 'com.android.application'

android {
    compileSdkVersion project.properties.compileSdkVersion.toInteger()
    defaultConfig {
        applicationId "com.termux.window"
        minSdkVersion project.properties.minSdkVersion.toInteger()
        targetSdkVersion project.properties.targetSdkVersion.toInteger()
        versionCode 14
        versionName "0.14"

        manifestPlaceholders.TERMUX_PACKAGE_NAME = "com.termux"
        manifestPlaceholders.TERMUX_APP_NAME = "Termux"
        manifestPlaceholders.TERMUX_FLOAT_APP_NAME = "Termux:Float"
    }

    signingConfigs {
        debug {
            storeFile file('dev_keystore.jks')
            keyAlias 'alias'
            storePassword 'xrj45yWGLbsO7W0v'
            keyPassword 'xrj45yWGLbsO7W0v'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false // Reproducible builds
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
            zipAlignEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == "debug") {
                def releaseTag = System.getenv("RELEASE_TAG")
                outputFileName = new File("termux-float" + (releaseTag ? "-" + releaseTag : "") + "-debug.apk")
            }
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.2.0'

    implementation 'com.termux.termux-app:termux-shared:dd952a90ad'
    implementation 'com.termux.termux-app:terminal-view:dd952a90ad'

    // Use if below libraries are published locally by termux-app with `./gradlew publishReleasePublicationToMavenLocal` and used with `mavenLocal()`.
    // If updates are done, republish there and sync project with gradle files here
    //implementation 'com.termux:termux-shared:0.117'
    //implementation 'com.termux:terminal-view:0.117'

    testImplementation 'junit:junit:4.13.2'
}

task versionName {
  doLast {
    print android.defaultConfig.versionName
  }
}
